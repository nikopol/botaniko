#!/usr/bin/env perl

###      #####    #   # # #  #
#  #  ##   #  ##  ##  #   # #
###  #  #  # #  # # # # # ##
#  # #  #  # #### #  ## # # #
###   ##   # #  # #   # # #  #

use strict;
use warnings;
use 5.010;
use Getopt::Long;
use Digest::SHA1 'sha1_hex';
use botaniko;

my %opt = ( 
	'db.disabled'   =>0,
	'db.trace_calls'=>0,
);

GetOptions(
	"help|h|?"          => \my $help,
	"config|cfg=s"      => \$opt{'config'},
	"server|s=s"        => \$opt{'server.host'},
	"port|p=i"          => \$opt{'server.port'},
	"nick|name|n=s"     => \$opt{'nick'},
	"channels|chan|c=s" => \$opt{'autojoin'},
	"log|loglevel|ll=s" => \$opt{'loglevel'},
	"dbdebug"           => \$opt{'db.trace_calls'},
	"dbinit"            => \$opt{'db.init'},
	"dbtrace"           => \$opt{'db.trace_calls'},
	"nodb"              => \$opt{'db.disabled'},
	"tweetid|tid=i"     => \$opt{'plugins.twitter.lastid'},
	"mute|quiet|silent" => \$opt{'mute'},
	"plugins=s"         => \$opt{'autoload'},
	"pass=s"            => \$opt{'passphrase'},
);

if( $help ) {
	print <<EOHELP
BoTaNiKo $botaniko::VERSION
le bot qui plante... ou pas

options (none required, default values indicated):

  -cfg=./botaniko.yml   config file
  -s=irc.freenode.net   irc server hostname
  -p=6667               irc server port
  -c=botaniko           channels to join separated with ,
  -ll=info              log level (error>warning>notice>info>debug)
  -mute                 setup mute mode
  -plugins=twitter,url  autoload these plugins
  -dbinit               erase and define elasticsearch botaniko's db
  -dbtrace              debug purpose
  -nodb                 start without db
  "-pass=pass phrase"   setup passphrase for root access

EOHELP
	;
	exit 1
}


if( $opt{autojoin} ) {
	$opt{autojoin} = [ map { m/^#/ ? $_ : "#$_" } split /,/,$opt{autojoin} ]
}
if( $opt{loglevel} ) {
	$opt{loglevel} = uc $opt{loglevel};
	$opt{loglevel} = 'DEBUG' unless $opt{loglevel} =~ /^(ERROR|WARN|WARNING|NOTICE|INFO|TWEET|DEBUG)$/;
}
if( $opt{autoload} ) {
	$opt{autoload} = [ split /,/,$opt{autoload} ]
}
if( $opt{passphrase} ) {
	$opt{passphrase} = sha1_hex $opt{passphrase};
}

plant %opt;

0
